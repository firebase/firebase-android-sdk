// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'firebase-library'
    id("kotlin-android")
    id 'LicenseResolverPlugin'
}

firebaseLibrary {
    libraryGroup "crashlytics"
    testLab {
        enabled = true
        device 'model=panther,version=33'
    }
}

android {
    adbOptions {
        timeOutInMs 60 * 1000
    }
    namespace "com.google.firebase.crashlytics"
    compileSdkVersion project.compileSdkVersion
    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionName version

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        androidTest {
            java {
                srcDir 'src/testUtil'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

thirdPartyLicenses {
    add 'Tape', "${projectDir}/third_party_licenses/tape/LICENSE"
}

dependencies {
    javadocClasspath 'com.google.code.findbugs:jsr305:3.0.2'

    api(project(":firebase-sessions"))
    api "com.google.android.gms:play-services-tasks:18.0.1"
    api 'com.google.firebase:firebase-annotations:16.2.0'
    api("com.google.firebase:firebase-common:21.0.0")
    api("com.google.firebase:firebase-common-ktx:21.0.0")
    api("com.google.firebase:firebase-components:18.0.0")
    api 'com.google.firebase:firebase-config-interop:16.0.0'
    api 'com.google.firebase:firebase-encoders:17.0.0'
    api 'com.google.firebase:firebase-encoders-json:18.0.0'
    api("com.google.firebase:firebase-installations:17.2.0")
    api 'com.google.firebase:firebase-installations-interop:17.1.0'
    api("com.google.firebase:firebase-measurement-connector:20.0.1")

    implementation 'com.google.android.datatransport:transport-api:3.0.0'
    implementation 'com.google.android.datatransport:transport-backend-cct:3.1.9'
    implementation 'com.google.android.datatransport:transport-runtime:3.1.9'
    implementation(libs.androidx.annotation)
    compileOnly(libs.errorprone.annotations)

    compileOnly 'com.google.auto.value:auto-value-annotations:1.6.5'

    annotationProcessor project(":encoders:firebase-encoders-processor")
    annotationProcessor 'com.google.auto.value:auto-value:1.6.5'

    testImplementation "androidx.test:core:$androidxTestCoreVersion"
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.4.3'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"

    androidTestImplementation "androidx.test:core:$androidxTestCoreVersion"
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation "androidx.test.ext:junit:$androidxTestJUnitVersion"
    androidTestImplementation 'com.google.firebase:firebase-encoders-json:18.0.0'
    androidTestImplementation 'com.google.protobuf:protobuf-java:3.21.9'
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:2.28.1'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.28.1'
    androidTestImplementation 'org.mockito:mockito-core:3.4.3'
    androidTestImplementation(libs.androidx.test.junit)
    androidTestImplementation(libs.androidx.test.runner)
    androidTestImplementation(libs.truth)
}
