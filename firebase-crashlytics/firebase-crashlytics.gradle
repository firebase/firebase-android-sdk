// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'firebase-library'
    id 'LicenseResolverPlugin'
}

firebaseLibrary {
    testLab {
        enabled = true
        device 'model=redfin,version=30'
    }
}

android {
    adbOptions {
        timeOutInMs 60 * 1000
    }

    compileSdkVersion 30
    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionName version

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        androidTest {
            java {
                srcDir 'src/testUtil'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

thirdPartyLicenses {
    add 'Tape', "${projectDir}/third_party_licenses/tape/LICENSE"
}

dependencies {
    compileOnly 'com.google.auto.value:auto-value-annotations:1.6.5'

    implementation project(':encoders:firebase-encoders')
    implementation project(':encoders:firebase-encoders-json')
    implementation project(':firebase-annotations')
    implementation project(':firebase-common')
    implementation project(':firebase-components')
    implementation project(':transport:transport-api')
    implementation project(':transport:transport-runtime')
    implementation project(':transport:transport-backend-cct')
    implementation project(':firebase-installations-interop')
    runtimeOnly project(':firebase-installations')
    implementation 'com.google.firebase:firebase-measurement-connector:18.0.2'
    implementation "com.google.android.gms:play-services-tasks:18.0.1"

    javadocClasspath 'com.google.code.findbugs:jsr305:3.0.2'

    annotationProcessor project(":encoders:firebase-encoders-processor")
    annotationProcessor 'com.google.auto.value:auto-value:1.6.5'

    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.4.3'


    androidTestImplementation project(':encoders:firebase-encoders-json')
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'org.mockito:mockito-core:3.4.3'
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:2.28.1'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.28.1'
    androidTestImplementation 'com.google.protobuf:protobuf-java:3.21.9'
}
