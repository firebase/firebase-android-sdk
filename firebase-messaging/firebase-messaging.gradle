// Copyright 2020 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'com.google.protobuf'
    id 'firebase-library'
    id("kotlin-android")
}
configurations.create("protobuild")

protobuf {
    dependencies {
        // Include the project dependency directly
        protobuild project(path: ':encoders:protoc-gen-firebase-encoders', configuration: 'shadow')
    }
    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        firebaseEncoders {
            path = project(':encoders:protoc-gen-firebase-encoders').buildDir.path + '/libs' +'/protoc-gen-firebase-encoders-all.jar'
        }
    }
    generateProtoTasks {

        all().each { task ->
            task.dependsOn configurations.protobuild
            task.inputs.file 'code_gen_cfg.textproto'
            task.plugins {
                firebaseEncoders {
                    option file('code_gen_cfg.textproto').path
                }
            }
            task.builtins {
                remove java
                remove kotlin
            }

        }
    }
}


firebaseLibrary {
    libraryGroup "messaging"
    testLab.enabled = false
}

android {
    adbOptions {
        timeOutInMs 60 * 1000
    }
    namespace "com.google.firebase.messaging"
    compileSdkVersion project.targetSdkVersion
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion project.targetSdkVersion
        versionName version

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions.unitTests.includeAndroidResources = true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    androidTestImplementation "androidx.annotation:annotation:1.0.0"
    androidTestImplementation "androidx.test.ext:junit:$androidxTestJUnitVersion"
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.mockito:mockito-core:2.25.0'
    androidTestImplementation 'org.mockito:mockito-inline:2.25.0'
    androidTestImplementation(project(":integ-testing")){
        exclude group: 'com.google.firebase', module: 'firebase-common'
        exclude group: 'com.google.firebase', module: 'firebase-components' }
    annotationProcessor project(":encoders:firebase-encoders-processor")
    implementation "com.google.android.gms:play-services-basement:18.1.0"
    implementation "com.google.android.gms:play-services-tasks:18.0.1"
    implementation "com.google.errorprone:error_prone_annotations:2.9.0"
    implementation "com.google.firebase:firebase-encoders-proto:16.0.0"
    implementation "com.google.firebase:firebase-iid-interop:17.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.google.android.datatransport:transport-api:3.1.0'
    implementation 'com.google.android.datatransport:transport-backend-cct:3.1.8'
    implementation 'com.google.android.datatransport:transport-runtime:3.1.8'
    implementation 'com.google.android.gms:play-services-base:18.0.1'
    implementation 'com.google.android.gms:play-services-cloud-messaging:17.1.0'
    implementation 'com.google.android.gms:play-services-stats:17.0.2'
    implementation 'com.google.firebase:firebase-encoders-json:18.0.0'
    implementation 'com.google.firebase:firebase-encoders:17.0.0'
    implementation(project(':firebase-datatransport')) {
         exclude group: 'com.google.firebase', module: 'firebase-common'
         exclude group: 'com.google.firebase', module: 'firebase-components'
     }
    implementation('com.google.firebase:firebase-installations-interop:17.1.0')
    implementation('com.google.firebase:firebase-measurement-connector:19.0.0')
    implementation("com.google.firebase:firebase-common:20.4.2")
    implementation("com.google.firebase:firebase-common-ktx:20.4.2")
    implementation("com.google.firebase:firebase-components:17.1.5")
    implementation("com.google.firebase:firebase-installations:17.2.0")
    javadocClasspath 'com.google.auto.value:auto-value-annotations:1.6.6'
    testAnnotationProcessor "com.google.auto.value:auto-value:1.6.3"
    testCompileOnly 'com.google.auto.value:auto-value-annotations:1.6.3'
    testImplementation "androidx.test:core:$androidxTestCoreVersion"
    testImplementation "com.google.truth:truth:$googleTruthVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation 'androidx.core:core:1.6.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    testImplementation 'androidx.test.ext:truth:1.4.0'
    testImplementation 'androidx.test.services:test-services:1.2.0'
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    testImplementation 'com.google.android.gms:play-services-cloud-messaging:17.0.1'
    testImplementation 'com.google.android.gms:play-services-vision:20.1.3'
    testImplementation 'com.google.guava:guava-testlib:12.0-rc2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'junit:junit:4.13-beta-2'
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation ("com.google.api-client:google-api-client:1.30.9") {
         exclude group: "org.apache.httpcomponents", module: "httpclient"
     }
    testImplementation ("com.google.firebase:firebase-iid:21.1.0") {
         exclude group: "com.google.firebase", module: "firebase-common"
         exclude group: "com.google.firebase", module: "firebase-components"
         exclude group: "com.google.firebase", module: "firebase-installations-interop"
         exclude group: "com.google.firebase", module: "firebase-installations"
     }
}
