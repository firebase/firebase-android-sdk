// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'firebase-library'
    id 'com.google.protobuf'
}

firebaseLibrary {
    testLab.enabled = true
    publishSources = true
    staticAnalysis.disableKotlinInteropLint()
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.21.0'
        }
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                grpc {
                    option 'lite'
                }
                javalite {}
            }
        }
    }
}
android {
    adbOptions {
        timeOutInMs 60 * 1000
    }

    compileSdkVersion project.targetSdkVersion
    defaultConfig {
        targetSdkVersion project.targetSdkVersion
        minSdkVersion project.minSdkVersion
        versionName version
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            proto {
                srcDir 'src/proto'
            }
            java {
            }
        }
        test {
            java {
                srcDir 'src/testUtil/java'
                srcDir 'src/roboUtil/java'
            }
        }
        androidTest {
            java {
                srcDir 'src/testUtil/java'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions.unitTests.includeAndroidResources = true
    ext.useProdBackendForTests = false
}

android.libraryVariants.all { variant ->
    if (findProperty("useProdBackendForTests")) {
        variant.buildConfigField("boolean", "USE_EMULATOR_FOR_TESTS", "false")
    } else {
        variant.buildConfigField("boolean", "USE_EMULATOR_FOR_TESTS", "true")
    }

}

configurations.all {
    if(it.name.contains('AndroidTestRuntimeClasspath')) {
        it.resolutionStrategy {
            force 'org.checkerframework:checker-compat-qual:2.5.5'
            force 'com.google.guava:guava:27.0.1-android'
        }
    }
}

dependencies {
    implementation project(':firebase-common')
    implementation project(':protolite-well-known-types')
    implementation project(':firebase-database-collection')


    //To provide @Generated annotations
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'io.grpc:grpc-stub:1.21.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.21.0'
    implementation 'io.grpc:grpc-okhttp:1.21.0'
    implementation 'io.grpc:grpc-android:1.21.0'
    implementation 'com.google.android.gms:play-services-basement:17.0.0'
    implementation 'com.google.android.gms:play-services-tasks:17.0.0'
    implementation 'com.google.android.gms:play-services-base:17.0.0'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation('com.google.firebase:firebase-auth-interop:18.0.0') {
        exclude group: "com.google.firebase", module: "firebase-common"
    }

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "com.google.truth:truth:$googleTruthVersion"
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    testImplementation 'com.google.guava:guava-testlib:12.0-rc2'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    androidTestImplementation "androidx.annotation:annotation:1.1.0"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        // TODO(wuandy): Also add "-Xlint:unchecked". But currently that
        // enables 100+ warnings due to our generated source code.
        options.compilerArgs << "-Xlint:deprecation" << "-Werror"
    }
}

// ==========================================================================
// Copy from here down if you want to use the google-services plugin in your
// androidTest integration tests.
// ==========================================================================
ext.packageName = "com.google.firebase.firestore"
apply from: '../gradle/googleServices.gradle'
