/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *
 * You may obtain a copy of the License at
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'firebase-library'
    id 'com.google.protobuf'
}

firebaseLibrary {
    testLab.enabled = true
    publishSources = true
    staticAnalysis.disableKotlinInteropLint()
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

android {
    compileSdkVersion project.targetSdkVersion
    defaultConfig {
      minSdkVersion 16
      targetSdkVersion project.targetSdkVersion
      multiDexEnabled true
      versionName version
      testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            proto {
                srcDir 'src/proto'
            }
        }

        androidTest.resources.srcDirs += ['src/androidTest/res']
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation project(':firebase-common')
    implementation project (':firebase-abt')

    implementation ('com.google.firebase:firebase-iid:18.0.0') {
        exclude group: "com.google.firebase", module: "firebase-common"
    }
    implementation 'com.google.firebase:firebase-measurement-connector:18.0.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation 'com.google.truth:truth:0.44'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.2'
    testImplementation "org.skyscreamer:jsonassert:1.5.0"

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'org.mockito:mockito-core:2.25.0'
    androidTestImplementation 'com.google.truth:truth:0.44'

    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:2.25.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.25.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "org.skyscreamer:jsonassert:1.5.0"
}
