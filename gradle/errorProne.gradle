// Copyright 2020 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

def isErrorProneRequested = getGradle().getStartParameter().getTaskNames().contains("withErrorProne")

def errorproneVersion = '2.4.0'
def errorproneJavacVersion = '9+181-r4173-1'

if (isErrorProneRequested) {
    configure(subprojects) {
        apply plugin: 'net.ltgt.errorprone'
        dependencies {
            errorprone "com.google.errorprone:error_prone_core:$errorproneVersion"
            errorproneJavac "com.google.errorprone:javac:$errorproneJavacVersion"

        }
        tasks.withType(JavaCompile) {
            options.errorprone.excludedPaths = '.*/build/generated/.*'
            options.errorprone.disable(
                    'JdkObsolete',
                    'ProtoTimestampGetSecondsGetNano',
            )
        }
    }
}

// marker task to enable errorprone diagnostics.
task withErrorProne {}
