// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

ext {
  /**
   * Parses the input file and returns a list of subprojects.
   *
   * <p>Expected file format:
   * <ul>
   *   <li>Empty lines are ignored.
   *   <li>Lines that start with # are ignored(considered comments).
   *   <li>Other lines are considered project paths with initial ":" removed.
   * </ul>
   */
  discoverSubprojects = {File subprojectsFile ->
    return subprojectsFile.readLines().collect {it.trim()}.findAll { !it.empty && !it.startsWith('#')}
  }

  /** Recursively renames build scripts to ${project.name}.gradle. */
  renameBuildScripts = {ProjectDescriptor project ->
    project.buildFileName = project.parent ? "${project.name}.gradle" : 'build.gradle'

    project.children.each {
      renameBuildScripts(it)
    }

  }
}
