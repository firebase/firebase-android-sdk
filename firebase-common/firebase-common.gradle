// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'firebase-library'
    id 'firebase-vendor'
}

firebaseLibrary {
    testLab.enabled = true
    publishSources = true
}

android {
    adbOptions {
        timeOutInMs 60 * 1000
    }

    compileSdkVersion project.targetSdkVersion
    defaultConfig {
      minSdkVersion project.minSdkVersion
      targetSdkVersion project.targetSdkVersion
      versionName version
      multiDexEnabled true
      testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
      consumerProguardFiles 'proguard.txt'
    }
    sourceSets {
        androidTest {
            java {
                srcDir 'src/testUtil'
            }
        }
        test {
            java {
                srcDir 'src/testUtil'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        baseline file("lint-baseline.xml")
    }
}

dependencies {
    implementation project(':firebase-annotations')
    implementation project(':firebase-components')
    implementation 'com.google.android.gms:play-services-basement:18.1.0'
    implementation "com.google.android.gms:play-services-tasks:18.0.1"
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'

    implementation 'javax.inject:javax.inject:1'
    vendor ('com.google.dagger:dagger:2.43.2') {
        exclude group: "javax.inject", module: "javax.inject"
    }
    annotationProcessor 'com.google.dagger:dagger-compiler:2.43.2'

    // FirebaseApp references storage, so storage needs to be on classpath when dokka runs.
    javadocClasspath project(path: ':firebase-storage')
    javadocClasspath 'com.google.auto.value:auto-value-annotations:1.6.6'

    compileOnly 'com.google.auto.value:auto-value-annotations:1.6.6'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // needed for Kotlin detection to compile, but not necessarily present at runtime.
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation 'junit:junit:4.12'
    testImplementation "com.google.truth:truth:$googleTruthVersion"
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.json:json:20210307'

    annotationProcessor 'com.google.auto.value:auto-value:1.6.5'

    androidTestImplementation project(':integ-testing')
    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'org.mockito:mockito-core:2.25.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:2.28.1'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.28.1'
}
