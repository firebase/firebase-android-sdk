// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id "org.gradle.kotlin.kotlin-dsl" version "1.2.6"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
}

repositories {
    mavenLocal()
    maven {
        url 'https://maven.google.com/'
    }
    jcenter()
    mavenCentral()

    maven {
        url 'https://storage.googleapis.com/android-ci/mvn/'
    }
}

// Refer latest "perf-plugin" released version on https://maven.google.com/web/index.html?q=perf-plugin#com.google.firebase:perf-plugin
// The System property allows us to integrate with an unreleased version from https://bityl.co/3oYt.
// Refer go/fireperf-plugin-test-on-head for more details.
def perfPluginVersion = System.getenv("FIREBASE_PERF_PLUGIN_VERSION") ?: "1.3.4"

dependencies {
    // Firebase performance plugin, it should be added here because of how gradle dependency
    // resolution works, otherwise it breaks Fireperf Test Apps.
    // See https://github.com/gradle/gradle/issues/12286
    implementation "com.google.firebase:perf-plugin:$perfPluginVersion"

    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'digital.wup:android-maven-publish:3.6.2'
    implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72'
    implementation 'org.json:json:20180813'
    implementation 'io.opencensus:opencensus-api:0.18.0'
    implementation 'io.opencensus:opencensus-exporter-stats-stackdriver:0.18.0'
    runtime 'io.opencensus:opencensus-impl:0.18.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation 'org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.17-g013-9b8280a'
    implementation 'com.android.tools.build:gradle:3.4.3'

    testImplementation 'junit:junit:4.13-rc-1'
    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation 'commons-io:commons-io:2.6'

}

gradlePlugin {
    plugins {
        licensePlugin {
            id = 'LicenseResolverPlugin'
            implementationClass = 'com.google.firebase.gradle.plugins.license.LicenseResolverPlugin'
        }
        publishingPlugin {
            id = 'PublishingPlugin'
            implementationClass = 'com.google.firebase.gradle.plugins.publish.PublishingPlugin'
        }
        firebaseLibraryPlugin {
            id = 'firebase-library'
            implementationClass = 'com.google.firebase.gradle.plugins.FirebaseLibraryPlugin'
        }

        firebaseJavaLibraryPlugin {
            id = "firebase-java-library"
            implementationClass = 'com.google.firebase.gradle.plugins.FirebaseJavaLibraryPlugin'
        }

        firebaseVendorPlugin {
            id = "firebase-vendor"
            implementationClass = 'com.google.firebase.gradle.plugins.VendorPlugin'
        }
    }
}

test {
    testLogging {
        // Make sure output from
        // standard out or error is shown
        // in Gradle output.
        showStandardStreams = true
    }
    enabled = rootProject.properties.get('enablePluginTests', false).toBoolean()
}
