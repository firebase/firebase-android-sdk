// Copyright 2020 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'firebase-library'
    id("kotlin-android")
    id 'firebase-vendor'
    id 'com.google.protobuf'
    id("copy-google-services")
}

firebaseLibrary {
    libraryGroup "ml-modeldownloader"
    testLab.enabled = true
    publishJavadoc = true
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}

android {
    namespace "com.google.firebase.ml.modeldownloader"
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        multiDexEnabled true
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions { jvmTarget = "1.8" }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        unitTests.all {
            jvmArgs '--add-opens=java.base/java.io=ALL-UNNAMED'
        }
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

thirdPartyLicenses {
    add 'Dagger', "${rootDir}/third_party/licenses/apache-2.0.txt"
}

dependencies {
    javadocClasspath 'com.google.code.findbugs:jsr305:3.0.2'

    vendor (libs.dagger.dagger) {
         exclude group: "javax.inject", module: "javax.inject"
     }

    api 'com.google.android.gms:play-services-tasks:18.0.1'
    api 'com.google.firebase:firebase-annotations:16.2.0'
    api("com.google.firebase:firebase-common:21.0.0")
    api("com.google.firebase:firebase-common-ktx:21.0.0")
    api("com.google.firebase:firebase-components:18.0.0")
    api('com.google.firebase:firebase-datatransport:18.2.0'){
         exclude group: 'com.google.firebase', module: 'firebase-common'
         exclude group: 'com.google.firebase', module: 'firebase-components'
     }
    api 'com.google.firebase:firebase-encoders:17.0.0'
    api 'com.google.firebase:firebase-encoders-json:18.0.0'
    api("com.google.firebase:firebase-installations:17.2.0")
    api 'com.google.firebase:firebase-installations-interop:17.1.0'

    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.google.android.datatransport:transport-api:3.0.0'
    implementation 'com.google.android.datatransport:transport-runtime:3.1.8'
    implementation 'com.google.auto.service:auto-service-annotations:1.0.1'
    implementation 'javax.inject:javax.inject:1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compileOnly "com.google.auto.value:auto-value-annotations:1.6.6"

    annotationProcessor project(":encoders:firebase-encoders-processor")
    annotationProcessor "com.google.auto.value:auto-value:1.6.5"
    annotationProcessor libs.dagger.compiler

    testImplementation(project(":integ-testing")) {
        exclude group: 'com.google.firebase', module: 'firebase-common'
        exclude group: 'com.google.firebase', module: 'firebase-components'
    }
    testImplementation "androidx.test:core:$androidxTestCoreVersion"
    testImplementation 'androidx.test:runner:1.5.1'
    testImplementation "androidx.test.ext:junit:$androidxTestJUnitVersion"
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.26.3'
    testImplementation "com.google.protobuf:protobuf-java-util:$protobufJavaUtilVersion"
    testImplementation "com.google.truth:truth:$googleTruthVersion"
    testImplementation 'com.google.truth.extensions:truth-proto-extension:1.0'
    testImplementation 'junit:junit:4.13-beta-2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"

    androidTestImplementation "androidx.annotation:annotation:1.1.0"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation "androidx.test.ext:junit:$androidxTestJUnitVersion"
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'junit:junit:4.13.1'
}
