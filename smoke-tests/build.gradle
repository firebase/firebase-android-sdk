// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths

buildscript {
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:3.4.2"
    classpath "com.google.gms:google-services:4.3.0"
  }
}

apply plugin: "com.android.application"

android {
  compileSdkVersion 29

  defaultConfig {
    minSdkVersion 16
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    versionName "1.0.0"
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      signingConfig signingConfigs.debug
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "smoke-test-rules.pro"
      testProguardFiles "infrastructure-rules.pro"
    }
  }

  if (project.hasProperty("testBuildType")) {
    testBuildType project.getProperty("testBuildType")
  }
}

apply from: "configure.gradle"

dependencies {
  // Firebase
  implementation "com.google.firebase:firebase-analytics"
  implementation "com.google.firebase:firebase-annotations"
  implementation "com.google.firebase:firebase-appindexing"
  implementation "com.google.firebase:firebase-auth"
  implementation "com.google.firebase:firebase-common"
  implementation "com.google.firebase:firebase-config"
  implementation "com.google.firebase:firebase-database"
  implementation "com.google.firebase:firebase-dynamic-links"
  implementation "com.google.firebase:firebase-firestore"
  implementation "com.google.firebase:firebase-functions"
  implementation "com.google.firebase:firebase-inappmessaging"
  // TODO(yifany): Restore after messaging in github is up to date
  // implementation "com.google.firebase:firebase-messaging"
  implementation "com.google.firebase:firebase-ml-vision"
  // TODO(b/174858514): Restore after M85 release is live
  // implementation "com.google.firebase:firebase-perf"
  implementation "com.google.firebase:firebase-storage"

  // Common utilities (application side)
  implementation "androidx.test:rules:1.2.0"
  implementation "androidx.test:runner:1.2.0"
  implementation "com.google.truth:truth:0.44"
  implementation "junit:junit:4.12"

  // Common utilities (instrumentation side)
  androidTestImplementation "androidx.test:core:1.2.0"
  androidTestImplementation "androidx.test:runner:1.2.0"
  androidTestImplementation "junit:junit:4.12"

  // TODO(yifany): Remove the temporary fix after b/164175567
  // https://firebase.google.com/support/release-notes/android#mlkit-self-serve-fixes
  implementation "com.google.android.gms:play-services-vision:20.1.1"
  implementation "com.google.android.gms:play-services-vision-common:19.1.1"
  implementation "com.google.android.gms:play-services-vision-image-label:18.0.5"
}

clean.doLast {
  def paths = Files.newDirectoryStream(Paths.get("."), "build-*")

  for (Path path : paths) {
    project.delete "$path/"
  }
}

apply plugin: "com.google.gms.google-services"
