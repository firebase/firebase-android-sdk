// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.


/**
 * Default build settings
 */
android {
    adbOptions {
        timeOutInMs 60 * 1000
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    compileSdkVersion project.targetSdkVersion

    defaultConfig {
        applicationId 'com.google.apksize'
        minSdkVersion project.targetSdkVersion
        multiDexEnabled true
        targetSdkVersion project.targetSdkVersion
        versionCode 1
        versionName '1.0'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    buildTypes {
        aggressive {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-aggressive.pro'
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
        }
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
        }
    }

    variantFilter { variant ->
        def buildTypeName = variant.buildType*.name
        if (buildTypeName.contains('debug')) {
            setIgnore(true)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
