// Copyright 2021 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'firebase-library'
}

firebaseLibrary {
    testLab.enabled = true
    publishSources = true
}

android {
    adbOptions {
        timeOutInMs 60 * 1000
    }

    compileSdkVersion project.targetSdkVersion
    defaultConfig {
        targetSdkVersion project.targetSdkVersion
        minSdkVersion project.minSdkVersion
        versionName version
        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "firebaseAppCheckDebugSecret", System.getenv("FIREBASE_APP_CHECK_DEBUG_SECRET") ?: ''
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions.unitTests.includeAndroidResources = true
}

dependencies {
    implementation project(':firebase-common')
    implementation project(':firebase-components')
    implementation project(':appcheck:firebase-appcheck')
    implementation project(':appcheck:firebase-appcheck-debug')
    implementation project(':appcheck:firebase-appcheck-interop')
    implementation 'com.google.android.gms:play-services-base:18.0.1'
    implementation 'com.google.android.gms:play-services-tasks:18.0.1'
    implementation 'androidx.test:core:1.2.0'

    javadocClasspath 'com.google.auto.value:auto-value-annotations:1.6.6'

    testImplementation 'junit:junit:4.13-beta-2'
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "com.google.truth:truth:$googleTruthVersion"
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation project(':appcheck:firebase-appcheck-safetynet')

    androidTestImplementation project(':firebase-storage')
    androidTestImplementation 'junit:junit:4.13-beta-2'
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'org.mockito:mockito-core:2.25.0'
}

// ==========================================================================
// Copy from here down if you want to use the google-services plugin in your
// androidTest integration tests.
// ==========================================================================
ext.packageName = "com.google.firebase.appcheck.debug.testing"
apply from: '../../gradle/googleServices.gradle'
