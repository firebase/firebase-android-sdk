// Copyright 2018 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'firebase-library'
    id 'com.google.protobuf'
    id 'firebase-vendor'
}

firebaseLibrary {
    testLab.enabled = true
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc {
                    option 'lite'
                }
            }
        }
    }
}

android {
    adbOptions {
        timeOutInMs 60 * 1000
    }

    compileSdkVersion project.targetSdkVersion
    defaultConfig {
      minSdkVersion 16
      targetSdkVersion project.targetSdkVersion
      multiDexEnabled true
      versionName version
      testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main {
            java {
            }
            proto {
                srcDir 'src/proto'
            }
        }
        androidTest {
            java {
                srcDir 'src/testUtil'
                srcDir 'src/testData'
            }
        }
        test {
            java {
                srcDir 'src/testUtil'
                srcDir 'src/testData'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        // Neccesary for Dagger
        incremental = false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations.all {
    if(it.name.contains('AndroidTestRuntimeClasspath')) {
        it.resolutionStrategy {
            force 'org.checkerframework:checker-compat-qual:2.5.5'
            force 'com.google.guava:guava:28.1-android'
        }
    }
}


thirdPartyLicenses {
    add 'Dagger', "file://${rootDir}/third_party/licenses/apache-2.0.txt"
}

dependencies {
    implementation project(':firebase-abt')
    implementation project(':firebase-common')
    implementation project(':firebase-components')
    implementation project(':protolite-well-known-types')
    implementation project(':transport:transport-api')
    implementation project(':firebase-datatransport')
    implementation project(':firebase-installations-interop')
    runtimeOnly project(':firebase-installations')
    implementation 'javax.inject:javax.inject:1'

    //To provide @Generated annotations
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation 'com.google.android.gms:play-services-tasks:17.0.2'
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation "io.grpc:grpc-protobuf-lite:$grpcVersion"
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.google.auto.value:auto-value-annotations:1.6.6'

    implementation('com.google.firebase:firebase-measurement-connector:18.0.0') {
        exclude group: 'com.google.firebase', module: 'firebase-common'
    }

    vendor ('com.google.dagger:dagger:2.27') {
        exclude group: "javax.inject", module: "javax.inject"
    }

    annotationProcessor 'com.google.dagger:dagger-compiler:2.27'
    annotationProcessor 'com.google.auto.value:auto-value:1.6.5'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.6'

    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation "com.google.truth:truth:$googleTruthVersion"
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation ("org.robolectric:robolectric:$robolectricVersion") {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    testImplementation "io.grpc:grpc-testing:$grpcVersion"
    testImplementation 'com.google.guava:guava:27.1-android'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'org.mockito:mockito-core:2.25.0'
    androidTestImplementation 'org.awaitility:awaitility:3.1.0'
    androidTestImplementation "io.grpc:grpc-testing:$grpcVersion"
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:2.25.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.25.0'

    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.27'
}
