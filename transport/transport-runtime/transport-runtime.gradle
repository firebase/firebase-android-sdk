// Copyright 2019 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id "com.google.protobuf"
    id 'firebase-library'
    id 'firebase-vendor'
}

// add a dependency on the protoc plugin's fat jar to make it available to protobuf below.
configurations.create("protobuild")
dependencies {
    protobuild project(path: ":encoders:protoc-gen-firebase-encoders", configuration: "shadow")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        firebaseEncoders {
            path = configurations.protobuild.asPath
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.dependsOn configurations.protobuild
            task.inputs.file 'code-gen-cfg.textproto'
            task.plugins {
                firebaseEncoders {
                    option file('code-gen-cfg.textproto').path
                }
            }
            task.builtins {
                remove java
            }
        }
    }
}

firebaseLibrary {
    publishJavadoc = false
    testLab {
        enabled = true

        // TODO(yifany): Blueline-28 was added in #4558, but has never passed.
        //               Disabling for now. Re-enable later if needed.
        // device 'model=blueline,version=28' // Pixel3
        device 'model=walleye,version=27' // Pixel2
        device 'model=Nexus4,version=22'
        device 'model=Nexus7,version=21'
        device 'model=Nexus4,version=19'
        device 'model=starqlteue,version=26' // Galaxy S9
        device 'model=harpia,version=23' // Moto G Play
    }
}

vendor {
    // Integration tests use dagger classes that are not used in the main SDK,
    // so we disable dead code elimination to ensure those classes are preserved.
    optimize = false
}

android {
    compileSdkVersion project.targetSdkVersion
    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto'
            }
        }
        androidTest {
            aidl {
                srcDir 'src/androidTest/aidl'
            }
        }
    }
    testOptions.unitTests {
        includeAndroidResources = true
    }
}

thirdPartyLicenses {
    add 'Dagger', "${rootDir}/third_party/licenses/apache-2.0.txt"
}

dependencies {
    implementation 'com.google.android.datatransport:transport-api:3.0.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.firebase:firebase-encoders:17.0.0'
    implementation "com.google.firebase:firebase-encoders-proto:16.0.0"
    annotationProcessor project(":encoders:firebase-encoders-processor")

    vendor ('com.google.dagger:dagger:2.27') {
        exclude group: "javax.inject", module: "javax.inject"
    }

    compileOnly "com.google.auto.value:auto-value-annotations:1.6.6"

    annotationProcessor "com.google.auto.value:auto-value:1.6.5"
    annotationProcessor 'com.google.dagger:dagger-compiler:2.27'

    testImplementation 'junit:junit:4.13-beta-2'
    testImplementation "com.google.truth:truth:$googleTruthVersion"
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation 'org.mockito:mockito-core:2.25.0'

    androidTestImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation "com.google.truth:truth:$googleTruthVersion"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'org.mockito:mockito-core:2.25.0'
    androidTestImplementation 'org.mockito:mockito-android:2.25.0'

    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.27'
}
